import org.jetbrains.intellij.IntelliJPluginExtension

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.17.0'
}

group 'com.github.idea-claude-code-gui'
version '0.0.9-beta3'

sourceCompatibility = 17
targetCompatibility = 17

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.10.1'
}

// 配置 IntelliJ Platform 插件
intellij {
    version = '2023.3.2'  // 目标 IDEA 版本
    type = 'IC'  // IC = IntelliJ IDEA Community Edition

    // 下载源码和 JavaDoc
    downloadSources = true

    // 插件依赖（如果需要）
    plugins = []
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

patchPluginXml {
    sinceBuild = '233'
    untilBuild = '253.*'

    changeNotes = """
        <ul>
            <li>初始版本发布</li>
            <li>实现右侧工具栏窗口</li>
            <li>最小支持IDEA 2023.3版本</li>
        </ul>
    """
}

runIde {
    // 启用 JCEF 支持
    jvmArgs '-Djcef.sandbox.enable=false'
}

buildSearchableOptions {
    enabled = false
}

// 统一的 AI Bridge（合并 Claude 和 Codex）
def aiBridgeDir = file("ai-bridge")
def aiBridgePackDir = file("$buildDir/ai-bridge-pack")
def aiBridgeArchive = new File(aiBridgePackDir, "ai-bridge.zip")

// 自动构建 webview
tasks.register('buildWebview', Exec) {
    workingDir file('webview')

    // 根据操作系统选择命令
    if (System.getProperty('os.name').toLowerCase().contains('windows')) {
        commandLine 'cmd', '/c', 'npm', 'run', 'build'
    } else {
        commandLine 'npm', 'run', 'build'
    }

    // 只在 webview 目录存在时执行
    onlyIf { file('webview').exists() }

    // 输出构建信息
    doFirst {
        println "Building webview..."
    }
    doLast {
        println "Webview build completed."
    }
}

tasks.register('packageAiBridge', Zip) {
    onlyIf { aiBridgeDir.exists() }
    archiveFileName = "ai-bridge.zip"
    destinationDirectory = aiBridgePackDir
    from(aiBridgeDir)
    exclude("node_modules/.pnpm/**")
    includeEmptyDirs = true
}

tasks.named("prepareSandbox") {
    dependsOn("buildWebview", "packageAiBridge")
    doLast {
        if (!aiBridgeDir.exists()) {
            throw new GradleException("ai-bridge 目录不存在，请确保已拉取并安装依赖。")
        }
        def sandboxRoot = layout.buildDirectory.dir("idea-sandbox").get().asFile
        def intellijExt = project.extensions.getByType(IntelliJPluginExtension)
        def pluginName = intellijExt.pluginName.getOrElse(project.name)
        def pluginDir = new File(sandboxRoot, "plugins/${pluginName}")

        // 部署 ai-bridge
        def extractedDir = new File(pluginDir, "ai-bridge")
        def archiveTarget = new File(pluginDir, "ai-bridge.zip")
        project.delete(extractedDir)
        project.delete(archiveTarget)
        project.copy {
            from(aiBridgeArchive)
            into(pluginDir)
        }
    }
}

tasks.named("buildPlugin") {
    dependsOn("packageAiBridge")
}
